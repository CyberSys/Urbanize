:Script_Queue1
00BF: 5@ = current_time_hours, 6@ = current_time_minutes
if or
5@ >= 20
5@ <= 4
then
    #CELLPHONE.Load()  
    #WBDYG1.Load()
    04ED: load_animation  "COP_AMBIENT"
    while true
        wait 0
        if and
        #CELLPHONE.Available()  
        #WBDYG1.Available()
        04EE:   animation "COP_AMBIENT" loaded
        then break
        end
    end  
    
    
    // -- Actor INIT  
    actor.Create(10@,civmale,#WBDYG1,1125.3217, -1127.6753, 23.8047)
    actor.Angle(10@) = 126.5
    
    0376: 11@ = create_random_actor_at 1124.9769 -1129.0 23.0
    0376: 12@ = create_random_actor_at 1125.3427 -1129.9 23.0  
    0376: 13@ = create_random_actor_at 1126.2548 -1130.5 23.0
    0376: 14@ = create_random_actor_at 1127.2627 -1131.2 23.0
    0376: 15@ = create_random_actor_at 1128.3729 -1131.8 23.0
    0376: 16@ = create_random_actor_at 1131.5227 -1131.8 23.0
    0376: 17@ = create_random_actor_at 1132.5637 -1131.8 23.0
    0376: 18@ = create_random_actor_at 1133.6558 -1131.8 23.0
    0376: 19@ = create_random_actor_at 1134.7734 -1131.8 23.0  
    0376: 20@ = create_random_actor_at 1135.8541 -1131.8 23.0
    
    for 25@ = -1 to 9
        085B: AS_actor 11@(25@,1i) set_kinda_stay_in_same_place 1
    end    
    for 25@ = 0 to 9
        0A8E: 24@ = 25@ + -1 
        0639: AS_actor 11@(25@,1i) rotate_to_actor 11@(24@,1i)
    end
    
    0850: AS_actor 20@ follow_actor 19@ 
    0850: AS_actor 19@ follow_actor 18@ 
    0850: AS_actor 18@ follow_actor 17@ 
    0850: AS_actor 17@ follow_actor 16@
    0850: AS_actor 16@ follow_actor 15@
    0850: AS_actor 15@ follow_actor 14@
    0850: AS_actor 14@ follow_actor 13@
    0850: AS_actor 13@ follow_actor 12@
    0850: AS_actor 12@ follow_actor 11@
    
    
    // Tasks                     
    05C9: AS_actor 10@ on_guard -1 ms
    
    // Olhar no relógio
    0615: ---- define_AS_pack_begin 27@ 
    0812: AS_actor -1 perform_animation "Coplook_in" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1 
    0812: AS_actor -1 perform_animation "Coplook_loop" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1 
    0812: AS_actor -1 perform_animation "Coplook_watch" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1  
    0812: AS_actor -1 perform_animation "Coplook_loop" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1  
    0812: AS_actor -1 perform_animation "Coplook_out" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1 
    0616: ---- define_AS_pack_end 27@
    
    // Balançar cabeça
    0615: ---- define_AS_pack_begin 28@ 
    0812: AS_actor -1 perform_animation "Coplook_in" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1 
    0812: AS_actor -1 perform_animation "Coplook_loop" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1 
    0812: AS_actor -1 perform_animation "Coplook_shake" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1   
    0812: AS_actor -1 perform_animation "Coplook_out" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1 
    0616: ---- define_AS_pack_end 28@
    
    // "nod"
    0615: ---- define_AS_pack_begin 29@ 
    0812: AS_actor -1 perform_animation "Coplook_in" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1 
    0812: AS_actor -1 perform_animation "Coplook_loop" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1 
    0812: AS_actor -1 perform_animation "Coplook_nod" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1   
    0812: AS_actor -1 perform_animation "Coplook_out" IFP "COP_AMBIENT" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1 
    0616: ---- define_AS_pack_end 29@
    
    
    while 00FE:   actor $player_actor sphere 0 in_sphere Queue1_X Queue1_Y Queue1_Z radius Queue1_R Queue1_R Queue1_R
    
        wait 500 //importante
        
        0209: 25@ = random_int_in_ranges 0 10 //ações comuns
        
        if or
        25@ == 1
        25@ > 7
        then
            0209: 25@ = random_int_in_ranges 0 10
            0A8E: 24@ = 25@ + -1
            0639: AS_actor 11@(25@,1i) rotate_to_actor 11@(24@,1i)
        else
            if 25@ == 2
            then                      
                0209: 25@ = random_int_in_ranges 0 10
                05C8: AS_actor 11@(25@,1i) look_around
            else  
                if 25@ == 3
                then                      
                    0209: 25@ = random_int_in_ranges 0 10
                    05C1: AS_actor 11@(25@,1i) speak_from_audio_table 161
                else  
                    if 25@ == 4
                    then                      
                        0209: 25@ = random_int_in_ranges 0 10
                        05C1: AS_actor 11@(25@,1i) speak_from_audio_table 162
                    end
                end
            end
        end
        
        wait 500
        
        0209: 25@ = random_int_in_ranges 0 40 //ações raras
        if 25@ == 1
        then        
            0209: 25@ = random_int_in_ranges 0 10
            0729: AS_actor 11@(25@,1i) hold_cellphone 1
            wait 4000        
            0729: AS_actor 11@(25@,1i) hold_cellphone 0
        else    
            if 25@ == 2
            then 
                0618: assign_actor 11@(25@,1i) to_AS_pack 27@
            else
                if 25@ == 3
                then  
                    0618: assign_actor 11@(25@,1i) to_AS_pack 28@
                else   
                    if 25@ == 4
                    then  
                        0618: assign_actor 11@(25@,1i) to_AS_pack 29@
                    end
                end
            end
        end
        
        00BF: 5@ = current_time_hours, 6@ = current_time_minutes
        if or
        5@ >= 20
        5@ <= 4
        then
        else //cancelado!                                   
            for 25@ = -1 to 9
                05DE: AS_actor 11@(25@,1i) walk_around_ped_path
            end                              
            for 25@ = -1 to 9
                01C2: remove_references_to_actor 11@(25@,1i)
            end
            9@ = true
        end
    end   
    
    
    // Release                         
    #CELLPHONE.Destroy()                      
    #WBDYG1.Destroy()
    04EF: release_animation "COP_AMBIENT"
    
    if 9@ <> true
    then
        for 25@ = 0 to 9
            034F: destroy_actor_with_fade 11@(25@,1i)
        end
    end
else
    while 00FE:   actor $player_actor sphere 0 in_sphere Queue1_X Queue1_Y Queue1_Z radius Queue1_R Queue1_R Queue1_R
        wait 0
    end
end     
wait 5000
0AC6: 0@ = label @Running_Queue1 offset
0A8C: write_memory 0@ size 1 value false vp 0
end_thread
