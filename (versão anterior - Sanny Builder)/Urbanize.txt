{$cleo}
0000:

const
ObjRadius = 150.0
end

while true
    wait 0
    player.Defined(0)
    jf continue
    
    //-- Triggers
    // Locations
    if 0AB1: call_scm_func @CheckStart_House1 0
    then 0AB1: call_scm_func @startNewThread 1 labelName @Script_House1
    end  
    if 0AB1: call_scm_func @CheckStart_House2 0
    then 0AB1: call_scm_func @startNewThread 1 labelName @Script_House2
    end   
    if 0AB1: call_scm_func @CheckStart_Queue1 0
    then 0AB1: call_scm_func @startNewThread 1 labelName @Script_Queue1
    end
    
    actor.StorePos($player_actor,1@,2@,3@)
    
    // Objects  
    
    if 0AE3: 0@ = random_object_near_point 1@ 2@ 3@ in_radius ObjRadius find_next 0
    then
        repeat
            0984: 4@ = object 0@ model
            if 4@ == 643 //kb_chr_tbl_test (mesa 4 cadeiras)
            then
                if 0AB1: call_scm_func @Store_Object 2 labelName @Running_Table1 ObjectHandle 0@ 
                then 0AB1: call_scm_func @startNewThread 2 labelName @Script_Table1 ObjectHandle 0@
                end
            end
            if 4@ == 1415 //dyn_dumpster (entulhos)
            then  
                if 0AB1: call_scm_func @Store_Object 2 labelName @Running_Dump1 ObjectHandle 0@ 
                then 0AB1: call_scm_func @startNewThread 2 labelName @Script_Dump1 ObjectHandle 0@
                end  
            end
        until 8AE3: not 0@ = random_object_near_point 1@ 2@ 3@ in_radius ObjRadius find_next 1
     end
end



//------- House 1 -------//
:CheckStart_House1
const
House1_X = 305.2728 
House1_Y = -1768.9983
House1_Z = 7.6797
House1_R = 200.0
end
if 00FE:   actor $player_actor sphere 0 in_sphere House1_X House1_Y House1_Z radius House1_R House1_R House1_R
then
    0AC6: 0@ = label @Running_House1 offset
    0A8D: 1@ = read_memory 0@ size 1 vp 0
    if 1@ == false
    then
        0A8C: write_memory 0@ size 1 value true vp 0
        0485:  return_true
        0AB2: ret 0
    end
end
059A:  return_false
0AB2: ret 0

:Running_House1
hex
00
end

{$I cleo\urbanize\Script_House1.txt}



//------- House 2 -------//
:CheckStart_House2
const
House2_X = 296.4674 
House2_Y = -1760.906 
House2_Z = 8.1118
House2_R = 200.0
end
if 00FE:   actor $player_actor sphere 0 in_sphere House2_X House2_Y House2_Z radius House2_R House2_R House2_R
then
    0AC6: 0@ = label @Running_House2 offset
    0A8D: 1@ = read_memory 0@ size 1 vp 0
    if 1@ == false
    then
        0A8C: write_memory 0@ size 1 value true vp 0
        0485:  return_true
        0AB2: ret 0
    end
end
059A:  return_false
0AB2: ret 0

:Running_House2
hex
00
end

{$I cleo\urbanize\Script_House2.txt}



//------- Queue 1 -------//
:CheckStart_Queue1
const
Queue1_X = 1124.9769 
Queue1_Y = -1129.0265 
Queue1_Z = 23.8047
Queue1_R = 150.0
end
if 00FE:   actor $player_actor sphere 0 in_sphere Queue1_X Queue1_Y Queue1_Z radius Queue1_R Queue1_R Queue1_R
then
    0AC6: 0@ = label @Running_Queue1 offset
    0A8D: 1@ = read_memory 0@ size 1 vp 0
    if 1@ == false
    then
        0A8C: write_memory 0@ size 1 value true vp 0
        0485:  return_true
        0AB2: ret 0
    end
end
059A:  return_false
0AB2: ret 0

:Running_Queue1
hex
00
end

{$I cleo\urbanize\Script_Queue1.txt}




//------- Table 1 -------//
:Running_Table1
hex //16 slots
00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 
00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 
00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 
00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00
end

{$I cleo\urbanize\Script_Table1.txt}



//------- Dump 1 -------//
:Running_Dump1
hex //16 slots
00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 
00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 
00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 
00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00
end

{$I cleo\urbanize\Script_Dump1.txt}





//------- Funcs -------//

:Store_Object
0AC6: 3@ = label 0@ offset
for 2@ = 0 to 64 step 4
    0A8E: 4@ = 3@ + 2@
    0A8D: 5@ = read_memory 4@ size 4 vp 0
    if 003B:   5@ == 1@
    then        
        break
    else
        if 5@ == 0
        then
            0A8C: write_memory 4@ size 4 value 1@ vp 0
            0485:  return_true
            0AB2: ret 0
        end
    end
end
059A:  return_false
0AB2: ret 0

:Remove_Object
0AC6: 3@ = label 0@ offset
for 2@ = 0 to 64 step 4
    0A8E: 4@ = 3@ + 2@
    0A8D: 5@ = read_memory 4@ size 4 vp 0
    if 003B:   5@ == 1@
    then        
        0A8C: write_memory 4@ size 4 value 1@ vp 0 
        break
    end
end
0AB2: ret 0


{$I cleo\urbanize\StartNewThread.txt}
